apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'realm-android'
apply from: '../app/coverage.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    namespace 'com.iceteaviet.fastfoodfinder'
    defaultConfig {
        applicationId "com.iceteaviet.fastfoodfinder"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 16
        versionName "19.07.03"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
        }
    }

    flavorDimensions "mock"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            dimension "mock"
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }

    aaptOptions {
        cruncherEnabled = false
    }

    dexOptions {
        javaMaxHeapSize = "4g"
    }

    lintOptions {
        abortOnError false
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

def build_param = "${build}"
if (build_param == "devCI") {
    // Exclude all except mockDebug flavor
    android.variantFilter { variant ->
        if (variant.getFlavors().get(0).name != 'mock' || variant.buildType.name != 'debug') {
            variant.setIgnore(true)
        }
    }
} else if (build_param == "releaseCI") {
    // Exclude all except prodRelease flavor
    android.variantFilter { variant ->
        if (variant.getFlavors().get(0).name != 'prod' || variant.buildType.name != 'release') {
            variant.setIgnore(true)
        }
    }
} else {
    // Normal local machine dev
    // Exclude mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //// Testing dependencies
    // Dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectricVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    testImplementation "org.powermock:powermock-module-junit4:$rootProject.ext.powerMockito"
    testImplementation "org.powermock:powermock-api-mockito2:$rootProject.ext.powerMockito"
    testImplementation "org.assertj:assertj-core:$rootProject.ext.assertJVersion"
    //testImplementation 'com.google.truth:truth:0.44' // Does not stable
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$rootProject.ext.kotlinMockitoVersion"

    // Espresso UI Testing dependencies.
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"

    // Android Testing Library's runner and rules
    androidTestImplementation "androidx.test:runner:$rootProject.ext.runnerVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.ext.rulesVersion"

    implementation "androidx.test.espresso:espresso-idling-resource:$rootProject.ext.espressoVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.ext.kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.ext.kotlinCouroutinesVersion"

    //// App dependencies
    // AndroidX
    implementation "androidx.appcompat:appcompat:$rootProject.appCompat"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.work:work-runtime-ktx:$workManager"
    implementation "androidx.work:work-rxjava2:$workManager"

    // Google Play Services
    implementation "com.google.android.gms:play-services-maps:$rootProject.playService"
    implementation "com.google.android.gms:play-services-location:$rootProject.playServiceLocation"
    implementation "com.google.android.gms:play-services-auth:$rootProject.playServiceAuth"
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    // Firebase
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-database:20.3.0'
    implementation 'com.google.firebase:firebase-auth:22.3.1'
    implementation 'com.google.firebase:firebase-perf:20.5.1'
    implementation 'com.google.firebase:firebase-crashlytics:18.6.1'

    // UI
    implementation 'com.google.android.material:material:1.11.0'
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    implementation "de.hdodenhof:circleimageview:$rootProject.circleImageViewVersion"

    // REST api
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"

    // Code flow
    implementation "org.greenrobot:eventbus:$rootProject.eventBusVersion"

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.5'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'

    // DB
    implementation "io.realm:realm-android-library:$rootProject.realmAndroidVersion"

    // FB SDK
    implementation "com.facebook.android:facebook-android-sdk:$rootProject.fbSdkVersion"

    // Logging
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"
}

kapt {
    generateStubs = true
}


apply plugin: 'com.google.gms.google-services'

// Apply the Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'
